buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url uri('http://dl.bintray.com/radimk/maven/')
        }
    }
    dependencies {
        classpath group: 'cz.kubacki.gradle.plugins',
                   name: 'gradle-nbm-plugin',
                version: '1.7.+'
        gradleApi()
    }
}
apply plugin: 'java'
apply plugin: 'nbm'

repositories {
    mavenCentral()
    maven {
        url 'http://bits.netbeans.org/maven2/'
    }
}

version = '1.0'
nbm {
    moduleName = 'org.nbgradle.netbeans.core'
    localizingBundle = 'org/nbgradle/netbeans/core/Bundle.properties'
    // Module signing data
    keyStore {
         keyStoreFile = project.file('keystore')
         username = 'myself'
         password = 'specialsauce'
    }
}

dependencies {
    // compile 'org.netbeans.api:org-openide-awt:RELEASE74'
    compile 'org.netbeans.api:org-openide-util:RELEASE80'
    compile 'org.netbeans.api:org-netbeans-api-annotations-common:RELEASE80'
    // example of dependency that will be copied to modules/ext and added to ClassPath: attribute in MANIFEST.MF
    compile 'org.slf4j:slf4j-api:1.7.2'
}

// set netbeansInstallDir project property

task netBeansRun(type: Exec) {
    doFirst {
        if (!project.hasProperty('netBeansInstallDir')) {
            throw new GradleException("missing netBeansInstallDir property")
        }
        def confFile = project.file("$project.buildDir/testuserdir/etc/netbeans.conf")
        confFile.parentFile.mkdirs()
        confFile.write "netbeans_extraclusters=\"$project.buildDir/module\""
    }

    workingDir "${project.buildDir}"

    //on windows:
    commandLine netBeansInstallDir + '/bin/netbeans', '--userdir', "$project.buildDir/testuserdir"
}
