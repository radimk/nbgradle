apply plugin: 'idea'

task downloadGradle(type: DownloadTask) {
    sourceUrl = 'https://services.gradle.org/distributions/gradle-2.2-rc-1-all.zip'
    target = file('gradle-2.2-rc-1-all.zip')
}
task unzipGradle(type: Copy) {
    def zipFile = downloadGradle.target
    destinationDir = file('unpacked/gradle-dist')

    from zipTree(zipFile)
    into destinationDir
}

unzipGradle.dependsOn downloadGradle

task testSetup(type: DefaultTask) {
    dependsOn 'unzipGradle'
}

task downloadNetBeans(type: DownloadTask) {
    sourceUrl = 'http://dlc.sun.com.edgesuite.net/netbeans/8.0/final/zip/netbeans-8.0-201403101706.zip'
    target = file('netbeans-8.0.zip')
}
task unzipNetBeans(type: Copy) {
    def zipFile = downloadNetBeans.target
    destinationDir = file('unpacked/netbeans')

    from zipTree(zipFile)
    into destinationDir
}

unzipNetBeans.dependsOn downloadNetBeans

task runSetup(type: DefaultTask) {
    dependsOn 'unzipNetBeans'
}

task netBeansRun(type: Exec) {
    dependsOn ':runSetup', ':core:netbeans', ':java:netbeans'
    doFirst {
        def confFile = project.file("$project.buildDir/testuserdir/etc/netbeans.conf")
        confFile.parentFile.mkdirs()
        def clusterPath = [':core', ':java'].collect { project.findProject(it).buildDir.path + '/module' }.join(':')
        confFile.write "netbeans_extraclusters=\"$clusterPath\""
    }

    workingDir "${project.buildDir}"

    commandLine rootProject.rootDir.absolutePath + '/unpacked/netbeans/netbeans/bin/netbeans', '--userdir', "$project.buildDir/testuserdir", '-J-Dnetbeans.console.logger=true', '-J-Dorg.nbgradle.netbeans.level=FINER'
}

idea {
    project {
        wildcards += ['?*.gradle']

        languageLevel = '1.7'
    }

    workspace.iws.withXml { provider ->
        Node node = provider.asNode()

        Node runManagerConfig = node.component.find { it.'@name' == 'RunManager' }

        // Add int test configuration to JUnit defaults
        Node runConfig = runManagerConfig.configuration.find { it.'@type' == 'JUnit' }
        Node vmParameters = runConfig.option.find { it.'@name' == 'VM_PARAMETERS' }

        def defaultTestVmParams = [
                "-DintegTest.gradleHomeDir=${file('unpacked/gradle-dist').absolutePath}",
                "-ea",
                "-XX:MaxPermSize=512m",
                "-Xmx512m"
        ]
        vmParameters.'@value' = defaultTestVmParams.collect { it.contains(" ") ? "\"$it\"" : it }.join(" ")
    }
}

class DownloadTask extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}

